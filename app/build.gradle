apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"
apply plugin: 'jacoco'


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "br.com.nerdrapido.mvvmmockapiapp"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "SERVER_URL", '"http://5b840ba5db24a100142dcd8c.mockapi.io/api/"')
        }
        debug {
            useLibrary 'android.test.runner'
            useLibrary 'android.test.base'
            useLibrary 'android.test.mock'
            buildConfigField("String", "SERVER_URL", '"http://5b840ba5db24a100142dcd8c.mockapi.io/api/"')
            testCoverageEnabled = true
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {

    // implementações básicas
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //  Implementação do Timber para logs
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Implementação do retrofit para a comunicação com a API
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // Implementação do Gson para a tradução de json
    implementation 'com.google.code.gson:gson:2.8.6'

    // Implementação do Glide para otenção e cache de URL de imagens
    def glide_version = '4.11.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Implementação do  Coroutines para trabalho assíncrono
    def coroutines_version = '1.3.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    // Implementação dos componentes de UI do android
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.1'

    // Implementação do android lifecycle
    def lifecycle_version = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Implementação do Koin para injeção de dependências
    def koin_version = '2.0.1'
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-core-ext:$koin_version"
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    // Implementação do Okhttplogger
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.0'


    // Test Core library
    testImplementation 'androidx.test:core:1.2.0'

    // Implementação de Test do Coroutines para trabalho assíncrono
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Implementação de Teste Junit
    def junit_version = '4.13'
    testImplementation "junit:junit:$junit_version"

    // Implementação de Teste Hamcrest
    testImplementation 'org.hamcrest:hamcrest-library:1.3'

    // mplementação de Teste  kotlintest
    def kotlintest_version = '3.4.2'
    testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintest_version"
    testImplementation "io.kotlintest:kotlintest-extensions-koin:$kotlintest_version"

    // Implementação de Teste do Koin
    testImplementation "org.koin:koin-test:$koin_version"

    // Implementação de Teste d Robolectric
    def robolectric_version = '4.3.1'
    testImplementation "org.robolectric:robolectric:$robolectric_version"

    // Implementação de Android Teste AndroidJUnitRunner and JUnit Rules
    def test_runner_version = '1.2.0'
    androidTestImplementation "androidx.test:runner:$test_runner_version"
    androidTestImplementation "androidx.test:rules:$test_runner_version"

    // Implementação do mockito
    def mockito_version = '2.21.0'
    testImplementation "org.mockito:mockito-inline:$mockito_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.mockito:mockito-android:$mockito_version"

    testImplementation "android.arch.core:core-testing:1.1.1"

}

task testAll(dependsOn: ['testDebugUnitTest']) {
    group = 'custom_tasks'
    description = "Run all tests"
}


jacoco {
    toolVersion = "0.8.5"
}

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {

    doFirst {
        clean
        testAll
    }

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugJava = fileTree(dir: "${buildDir}/intermediates/classes/debugUnitTest", excludes: fileFilter)
    def debugKotlin = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    getSourceDirectories().setFrom(files([mainSrc]))
    getClassDirectories().setFrom(files([debugJava, debugKotlin]))
    getExecutionData().setFrom(files("${buildDir}/jacoco/testDebugUnitTest.exec"))
}